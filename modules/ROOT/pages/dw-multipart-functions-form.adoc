= form
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]



[[form1]]
== form&#40;Array<MultipartPart&#62;&#41;: Multipart

Creates a `Multipart` data structure using a specified array of parts. In this example the multipart structure is written out manually with a root element named `parts`. 


=== Parameters

[%header, cols="1,3"]
|===
| Name | Description
| `parts` | An array of parts (`MultipartPart` data structures).
|===

=== Example

This example creates a `Multipart` data structure that contains parts, which
are described in examples for the `field` function. For additional uses of
`form`, see examples in the Multipart `file` and `field` documentation.

You can also manually construct a data structure compatible with `multipart/form-data` output. 

In this example, the first part uses the `Multipart::file()` function to import an external file named `orders.xml` file. In this case, the path is internal to the Mule application, where the file is located in the `src/main/resources` folder. See the `file` docs for more details on this example. 

The second part uses the `Multipart::field()` function. It uses the version of the function where the field names are included in the input parameter as an object of key/value pairs, so the keys can be arranged in any order. In this example, and additional key named `extra` is included, and it is ignored by the `Multipart::field()` function. 

The third part uses the more compact version of the `Multipart::field()` function where the required and optional parameters are entered, in the correct order, as input parameters. The first three parameters `name`, `value`, and `mime` are required. The `fileName` parameters is optional, and should only be used if the content came from a file or should be written to a file.  

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
import dw::module::Multipart
output multipart/form-data

var myOrders = "./orders.xml"
var fileArgs = { name: "file", path: myOrders, mime: "application/xml", fileName: "myorders.xml"}
var fieldArgs = {name:"userName",value: "Annie Point", mime: "text/plain", extra: "ignored"}
---
Multipart::form([
  Multipart::file(fileArgs),
  Multipart::field(fieldArgs),
  Multipart::field("myJson", {"user": "Annie Point"}, "application/json", "userInfo.json")
])
----

In this manually constructed structure, the part keys `part1` and `part2` are stripped out in the `multipart/form-data` output. 

==== Output

[source,json,linenums]
----
------=_Part_146_143704079.1560394078604
Content-Type: application/xml
Content-Disposition: form-data; name="file"; filename="myorders.xml"

<?xml version='1.0' encoding='UTF-8'?>
<orders>
  <order>
    <item>
      <id>1001</id>
      <qty>1</qty>
      <price>$100</price>
    </item>
    <item>
      <id>2001</id>
      <qty>2</qty>
      <price>$50</price>
    </item>
  </order>
</orders>
------=_Part_146_143704079.1560394078604
Content-Type: text/plain
Content-Disposition: form-data; name="userName"

Annie Point
------=_Part_146_143704079.1560394078604
Content-Type: application/json
Content-Disposition: form-data; name="myJson"; filename="userInfo.json"

{
  "user": "Annie Point"
}
------=_Part_146_143704079.1560394078604--
----

=== Example

You can also manually construct a data structure compatible with `multipart/form-data` output, without using the `form` function. 

Ths example constructs a data structure using DataWeave code that is compatible with the `multipart/form-data` output format. 

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
import dw::module::Multipart
output multipart/form-data
var firstPart = "content for my first part"
var secondPart = "content for my second part"
---
{
  parts: {
    part1: Multipart::field({name:"myFirstPart",value: firstPart}),
    part2: Multipart::field("mySecondPart", secondPart)
  }
}
----

In this manually constructed structure, the part keys `part1` and `part2` are stripped out in the `multipart/form-data` output. 

==== Output

[source,json,linenums]
----
------=_Part_320_1528378161.1542639222352
Content-Disposition: form-data; name="myFirstPart"

content for my first part
------=_Part_320_1528378161.1542639222352
Content-Disposition: form-data; name="mySecondPart"

content for my second part
------=_Part_320_1528378161.1542639222352--
