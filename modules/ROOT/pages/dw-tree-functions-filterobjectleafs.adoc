= filterObjectLeafs
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]



[[filterobjectleafs1]]
== filterObjectLeafs&#40;value: Any, criteria: &#40;value: Any, path: Path&#41; &#45;&#62; Boolean&#41;: Any

Applies a filtering expression to leaf or `Path` values of keys in
an object.


The leaf values in the object must be `SimpleType` or `Null` values. See
https://docs.mulesoft.com/mule-runtime/4.3/dw-core-types[Core Types]
for descriptions of the types.

_Introduced in DataWeave 2.4.0. Supported by Mule 4.4.0 and later._

=== Parameters

[%header, cols="1,3"]
|===
| Name   | Description
| `value` | An input value of `Any` type.
| `criteria` | Boolean expression to apply to `SimpleType` or `Null`
               leaf values of all objects in the input `value`. If the
               result is `true`, the object retains the leaf value and
               its key. If not, the function removes the leaf value
               and key from the output.
|===

=== Example

This example shows how `filterObjectLeafs` behaves with different inputs.

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
import * from dw::util::Tree
var myArray = [{name @(mail: "me@me.com", test:123 ): "", id:"test"},
               {name: "Me", id:null}]
output application/json
---
{
 a: {
     name: "Mariano",
     lastName: null,
     age: 123,
     friends: myArray
    }  filterObjectLeafs ((value, path) ->
         !(value is Null or value is String)),
 b: { c : null, d : "hello" } filterObjectLeafs ((value, path) ->
         (value is Null and isObjectType(path)))
}
----

==== Output

[source,Json,linenums]
----
{
  "a": {
    "age": 123,
    "friends": [
      {

      },
      {

      }
    ]
  },
  "b": {
    "c": null
  }
}
----

