= distinctBy
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]



[[distinctby1]]
== distinctBy&#40;Array<T&#62;, &#40;item: T, index: Number&#41; &#45;&#62; Any&#41;: Array<T&#62;

Iterates over an array and returns the unique elements in it.


This version of `distinctBy` finds unique values in an array. Other versions
act on an object and handle a null value.

=== Parameters

[%header, cols="1,3"]
|===
| Name   | Description
| `items` | The array to evaluate.
| `criteria` | The criteria used to select an `item` and/or `index` from the array.
|===

=== Example

This example inputs an array that contains duplicate numbers and returns an
array with unique numbers from that input. Note that you can write the same
expression using an anonymous for the values:
`[0, 1, 2, 3, 3, 2, 1, 4] distinctBy &#36;`

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
[0, 1, 2, 3, 3, 2, 1, 4] distinctBy (value) -> { "unique" : value }
----

==== Output

[source,JSON,linenums]
----
[ 0, 1, 2, 3, 4]
----

=== Example

This example removes duplicates of `"Kurt Cagle"` from an array.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
var record =  {
  "title": "XQuery Kick Start",
  "author": [
    "James McGovern",
    "Per Bothner",
    "Kurt Cagle",
    "James Linn",
    "Kurt Cagle",
    "Kurt Cagle",
    "Kurt Cagle",
    "Vaidyanathan Nagarajan"
  ],
  "year":"2000"
}
---
{
    "book" : {
      "title" : record.title,
      "year" : record.year,
      "authors" : record.author distinctBy $
    }
}
----

==== Output

[source,JSON,linenums]
----
{
  "book": {
    "title": "XQuery Kick Start",
    "year": "2000",
    "authors": [
      "James McGovern",
      "Per Bothner",
      "Kurt Cagle",
      "James Linn",
      "Vaidyanathan Nagarajan"
    ]
  }
}
----


[[distinctby2]]
== distinctBy&#40;Null, &#40;item: Nothing, index: Nothing&#41; &#45;&#62; Any&#41;: Null

Helper function that allows *distinctBy* to work with null values.


[[distinctby3]]
== distinctBy&#40;{ &#40;K&#41;?: V }, &#40;value: V, key: K&#41; &#45;&#62; Any&#41;: Object

Removes duplicate key-value pairs from an object.


=== Parameters

[%header, cols="1,3"]
|===
| Name   | Description
| `object` | The object from which to remove the key-value pairs.
| `criteria` | The `key` and/or `value` used to identify the key-value pairs to remove.
|===

=== Example

This example inputs an object that contains duplicate key-value pairs and
returns an object with key-value pairs from that input. Notice that the
keys (`a` and `A`) are not treated with case sensitivity, but the values
(`b` and `B`) are. Also note that you can write the same expression using
an anonymous for the values:
`{a : "b", a : "b", A : "b", a : "B"} distinctBy &#36;`

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
{a : "b", a : "b", A : "b", a : "B"} distinctBy (value) -> { "unique" : value }
----

==== Output

[source,JSON,linenums]
----
{ "a": "b", "a": "B" }
----

=== Example

This example removes duplicates (`<author>James McGovern</author>`)
from `<book/>`.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/xml
---
{
   book : {
     title : payload.book.title,
     authors: payload.book.&author distinctBy $
   }
}
----

==== Input

[source,XML,linenums]
----
<book>
  <title> "XQuery Kick Start"</title>
  <author>James Linn</author>
  <author>Per Bothner</author>
  <author>James McGovern</author>
  <author>James McGovern</author>
  <author>James McGovern</author>
</book>
----

==== Output

[source,XML,linenums]
----
<book>
  <title> "XQuery Kick Start"</title>
  <authors>
      <author>James Linn</author>
      <author>Per Bothner</author>
      <author>James McGovern</author>
  </authors>
</book>
----


