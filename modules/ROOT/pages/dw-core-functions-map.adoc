= map
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]



[[map1]]
== map&#40;Array<T&#62;, &#40;item: T, index: Number&#41; &#45;&#62; R&#41;: Array<R&#62;

Iterates over items in an array and outputs the results into a new array.


=== Parameters

[%header, cols="1,3"]
|===
| Name   | Description
| `items` | The array to map.
| `mapper` | Expression or selector used to act on each `item` and optionally,  each `index` of that item.
|===

=== Example

This example iterates over an input array (`["jose", "pedro", "mateo"]`) to
produce an array of DataWeave objects. The anonymous function
`(value, index) -> {index: value}` maps each item in the input to an object.
As `{index: value}` shows, each index from the input array becomes a key
for an output object, and each value of the input array becomes the value of
that object.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
["jose", "pedro", "mateo"] map (value, index) -> { (index) : value}
----

==== Output

[source,JSON,linenums]
----
[ { "0": "jose" }, { "1": "pedro" }, { "2": "mateo" } ]
----

=== Example

This example iterates over the input array (`['a', 'b', 'c']`) using
an anonymous function that acts on the items and indices of the input. For
each item in the input array, it concatenates the `index + 1` (`index` plus 1)
with an underscore (`_`), and the corresponding `value` to return the array,
`[ "1_a", "2_b", "3_c" ]`.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
['a', 'b', 'c'] map ((value, index) -> (index + 1) ++ '_' ++ value)
----

==== Output

[source,JSON,linenums]
----
[ "1_a", "2_b", "3_c" ]
----

=== Example

If the parameters of the `mapper` function are not named, the index can be
referenced with `&#36;&#36;`, and the value with `&#36;`. This example
iterates over each item in the input array `['joe', 'pete', 'matt']`
and returns an array of objects where the index is selected as the key.
The value of each item in the array is selected as the value of
the returned object. Note that the quotes around `&#36;&#36;` 
are necessary to convert the numbers to strings. 

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
['joe', 'pete', 'matt'] map ( "$$" : $)
----

==== Output

[source,JSON,linenums]
----
[
  { "0": "joe" },
  { "1": "pete" },
  { "2": "matt" }
]
----


[[map2]]
== map&#40;Null, &#40;item: Nothing, index: Nothing&#41; &#45;&#62; Any&#41;: Null

Helper function that allows `map` to work with `null` values.
