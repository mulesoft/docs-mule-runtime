= map
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]



[[map1]]
== map&#40;Array<T&#62;, &#40;item: T, index: Number&#41; &#45;&#62; R&#41;: Array<R&#62;

Transforms items from the given list (array) into a new list using the
specified mapper function.

=== Parameters

[%header, cols="1,3"]
|===
| Name   | Description
| `items` | The array to map.
| `mapper` | Expression or selector used to act on each `item` and optionally,  each `index` of that item.
|===

=== Example

This example iterates over a input array (`["jose", "pedro", "mateo"]`) to
produce an array of objects. The anonymous function
`(value, index) -> {index: value}` maps each item in the input to an object.
As `{index: value}` shows, each index from the input array becomes a key
for an output object, and each value of the input array becomes the value of
that object.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
["jose", "pedro", "mateo"] map (value, index) -> { (index) : value}
----

==== Output

[source,JSON,linenums]
----
[ { "0": "jose" }, { "1": "pedro" }, { "2": "mateo" } ]
----

=== Example

This example iterates over the input array (`['a', 'b', 'c']`) using
an anonymous function that acts on the items and indices of the input. For
each item in the input array, it concatenates the `index + 1` (`index` plus 1)
with an underscore (`_`), and the corresponding `value` to return the array,
`[ "1_a", "2_b", "3_c" ]`.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
['a', 'b', 'c'] map ((value, index) -> (index + 1) ++ '_' ++ value)
----

==== Output

[source,JSON,linenums]
----
[ "1_a", "2_b", "3_c" ]
----

=== Example

If the parameters of the `mapper` function are not named, the index can be
referenced with `&#36;&#36;`, and the value with `&#36;`, so the first
example can be written as shown next. Note that the concatenation function
(`++`) in the mapping expression is written in prefix notation so that the
reference of both anonymous parameters can be read.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
['joe', 'pete', 'matt'] map ++(($$ + 1) ++ '_', $)
----

==== Output

[source,JSON,linenums]
----
[ "1_joe", "2_pete", "3_matt" ]
----


[[map2]]
== map&#40;Null, &#40;item: Nothing, index: Nothing&#41; &#45;&#62; Any&#41;: Null

Helper function that allows `map` to work with null values.
