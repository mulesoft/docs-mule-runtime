= DataWeave Operators
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]

DataWeave 2.0 supports several mathematical, equality, relational, and logical
operators.

== Mathematical Operators

DataWeave supports the most common mathematical operators:

[%header,cols="1,2"]
|===
| Operator | Description
| `+` | For addition
| `-` | For subtraction
| `*` | For multiplication.
| `/`  | For division.
|===

These examples use mathematical operators:

.Source
[source,dataweave,linenums]
----
%dw 2.0
output application/json
---
{ "math" : [
    { "2 + 2" : (2 + 2) },
    { "2 - 2" : (2 - 2) },
    { "2 * 2" : (2 * 2) },
    { "2 / 2" : (2 / 2) }
  ]
}
----

.Output
[source,json,linenums]
----
"math": [
    { "2 + 2": 4 },
    { "2 - 2": 0 },
    { "2 * 2": 4 },
    { "2 / 2": 1.0 }
  ]
}
----

Note that several DataWeave functions operate on numbers, for example: xref:dw-core-functions-sum.adoc[sum], xref:dw-core-functions-mod.adoc[mod] (for modulo), and xref:dw-core-functions-abs.adoc[avg] (for average).

== Equality and Relational Operators

DataWeave supports the following equality and relational operators:

[%header,cols="1,2"]
|===
| Operator | Description
| `<` | For less than.
| `>` | For greater than.
| `&lt;=` | For less than or equal to.
| `>=` | For greater than or equal to.
| `==` | For equal to.
| `~=` | Equality operator that tries to coerce one value to the type of the other when the types are different.
|===

Note that you can negate these operators by using the <<logical_operator, logical operator>>, `not`. 

These examples use relational operators:

.Source
[source,dataweave,linenums]
----
%dw 2.0
output application/json
---
{ "relational" : [
    { "1 < 1" : (1 < 1) },
    { "1 > 2" : (1 > 2) },
    { "1 <= 1" : (1 <= 1) },
    { "1 >= 1" : (1 >= 1) }
  ]
}
----

.Output
[source,json,linenums]
----
"relational": [
    { "(1 < 1)": false },
    { "(1 > 2)": false },
    { "(1 <= 1)": true },
    { "(1 >= 1)": true }
  ]
}
----

These examples use equality operators:

.Source
[source,dataweave,linenums]
----
%dw 2.0
output application/dw
---
{ "equality" :
  [
    (1 == 1),
    (1 == 2),
    ("true" == true),
    ("true" ~= true),
    (['true'] ~= [true]),
    ('1' ~= 1)
  ]
}
----

.Output
[source,text,linenums]
----
{
  equality: [ true, false, false, true, true, true ]
}
----

[[logical_operators]]
== Logical Operators

The following logical operators are supported.

[%header,cols="1,2"]
|===
| Operator | Description
| `not` | Negates the result of the input.
| `and` | Returns `true` if the result of all inputs is true, `false` if not.
| `or` | Returns `true` if the result of any input is true, `false` if not.
|===

These examples show uses of these operators:

.Source
[source,dataweave,linenums]
----
%dw 2.0
output application/json
var myArray = [1,2,3,4,5]
var myMap = myArray map not (($ mod 2) == 0)
---
{
  "not" : [
    "notTrue" : not true,
    "notFalse" : not false,
    "myMapWithNot" : myMap
  ],
  "and" : [
    "andTrueFalse" : true and false,
    "andIsTrue" : (1 + 1 == 2) and (2 + 2 == 4),
    "andIsFalse" : (1 + 1 == 2) and (2 + 2 == 2)
  ],
  "or" : [
    "orTrueFalse" : true or false,
    "orIsTrue" : (1 + 1 == 2) or (2 + 2 == 2),
    "orIsFalse" : (1 + 1 == 1) or (2 + 2 == 2)
  ]
}
----

Note that `myMap` iterates through the items in a list (`myArray`) and
determines whether the modulo (`mod`) expression _does not_ evaluate to `0` when
applied to each given item.

.Output
[source,json,linenums]
----
{
  "not": [
    { "notTrue": false },
    { "notFalse": true },
    { "myMapWithNot": [ true, false, true, false, true ] }
  ],
  "and": [
    { "andTrueFalse": false },
    { "andIsTrue": true },
    { "andIsFalse": false }
  ],
  "or": [
    { "orTrueFalse": true },
    { "orIsTrue": true },
    { "orIsFalse": false }
  ]
}
----

Note that `not` works in expressions such as `not (true)`, but `not(true)`
(without the space) does not work.

You can use logical operators together. The next example uses `or not` as defined in the `orNot` expression, uses `and not` in `andNot`, and uses `not` and `and not` in `notWithAndNot`.

.Example: Using Logical Operators Together
[source,dataweave,linenums]
----
%dw 2.0
output application/json
var orNot = if (1 + 1 == 4 or not 1 == 2) {"answer": "foo"}
             else {"answer": "nope"}
var andNot = if (1 + 1 == 2 and not 1 == 2) {"answer": "bar"}
             else {"answer": "nope"}
var notWithAndNot = if (not (1 + 1 == 2 and not 1 == 1)) {"answer": "foobar"}
              else {"answer": "nope"}
---
{ "answers" :
  [
    orNot,
    andNot,
    notWithAndNot
  ]
}
----

.Output
[source,json,linenums]
----
{
  "answers": [
    { "answer": "foo" },
    { "answer": "bar" },
    { "answer": "foobar" }
  ]
}
----
