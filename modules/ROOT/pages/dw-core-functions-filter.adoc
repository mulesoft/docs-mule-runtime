= filter
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]


[[filter1]]
== filter(Array<T>, (item: T, index: Number) -> Boolean): Array<T>

Filters a list (array) by applying an expression that returns matching items in the list.


The expression must return `true` or `false`. If the expression returns `true` for an element, the element will remain in the list. If it returns `false` for an element, the element will be filtered out of the results.

=== Parameters

[%header, cols="1,3"]
|===
| Name | Description
| `items` | The list (array) to filter.
| `criteria` | Function that receives an `item` and `index` and returns `true` if the element should remain.
|===

=== Example

This example returns an array of all elements greater than `2`.

==== Source

[source,DataWeave, linenums]
----
[9,2,3,4,5] filter (myitem, myindex) -> (myitem > 2)
----

==== Output

[source,JSON,linenums]
----
[9,3,4,5]
----

=== Example

This example returns an array of all elements found at an index
greater than `2`.

==== Source

[source,DataWeave, linenums]
----
[9,2,3,4,5] filter ((myitem, myindex) -> (myindex > 2))
----

==== Output

[source,JSON,linenums]
----
[4,5]
----

=== Example

This example returns an array of all elements found at an index (`&#36;&#36;`)
greater than `1` where the value of the element is less than `5`.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
[9, 2, 3, 4, 5] filter (($$ > 1) and ($ < 5))
----

==== Output

[source,JSON,linenums]
----
[3,4]
----


[[filter2]]
== filter(Null, (item: Nothing, index: Nothing) -> Boolean): Null

Helper function that allows `filter` to work with null values.
