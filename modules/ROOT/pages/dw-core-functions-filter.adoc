= filter
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]



[[filter1]]
== filter<T&#62;&#40;@StreamCapable items: Array<T&#62;, criteria: &#40;item: T, index: Number&#41; &#45;&#62; Boolean&#41;: Array<T&#62;

Iterates over an array and applies an expression that returns matching values.


The expression must return `true` or `false`. If the expression returns `true`
for a value or index in the array, the value gets captured in the output array.
If it returns `false` for a value or index in the array, that item gets
filtered out of the output. If there are no matches, the output array will
be empty.

=== Parameters

[%header, cols="1,3"]
|===
| Name | Description
| `items` | The array to filter.
| `criteria` | Boolean expression that selects an `item` and/or `index`.
|===

=== Example

This example returns an array of values in the array that are greater than `2`.

==== Source

[source,DataWeave, linenums]
----
[9,2,3,4,5] filter (value, index) -> (value > 2)
----

==== Output

[source,JSON,linenums]
----
[9,3,4,5]
----

=== Example

This example returns an array of all the users with age bigger or equal to 30.
It shows that inside the filter lambda, the data of each element can be access.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
---
[{name: "Mariano", age: 37}, {name: "Shoki", age: 30}, {name: "Tomo", age: 25}, {name: "Ana", age: 29}]
          filter ((value, index) -> value.age >= 30)
----

==== Output

[source,JSON,linenums]
----
[
   {
     "name": "Mariano",
     "age": 37
   },
   {
     "name": "Shoki",
     "age": 30
   }
]
----

=== Example

This example returns an array of all items found at an index (`&#36;&#36;`)
greater than `1` where the value of the element is less than `5`. Notice that
it is using anonymous parameters as selectors instead of using named
parameters in an anonymous function.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
[9, 2, 3, 4, 5] filter (($$ > 1) and ($ < 5))
----

==== Output

[source,JSON,linenums]
----
[3,4]
----

=== Example

This example reads a JSON array that contains objects with `user` and `error` keys, and uses the `filter` function to return only the objects in which the value of the `error` key is `null`.

==== Source
[source,DataWeave, linenums]
----
%dw 2.0
output application/json

var users = [
   {
      "user": {
         "name": "123",
         "lastName": "Smith"
      },
      "error": "That name doesn't exists"
   },
   {
      "user": {
         "name": "John",
         "lastName": "Johnson"
      },
      "error": null
   }
]
---
users filter ((item, index) -> item.error == null)
----

==== Output
[source,JSON,linenums]
----
[
  {
    "user": {
      "name": "John",
      "lastName": "Johnson"
    },
    "error": null
  }
]
----


[[filter2]]
== filter&#40;@StreamCapable text: String, criteria: &#40;character: String, index: Number&#41; &#45;&#62; Boolean&#41;: String

Iterates over a string and applies an expression that returns matching values.


The expression must return `true` or `false`. If the expression returns `true`
for a character or index in the array, the character gets captured in the output string.
If it returns `false` for a character or index in the array, that character gets
filtered out of the output. If there are no matches, the output string will
be empty.

=== Parameters

[%header, cols="1,3"]
|===
| Name   | Description
| text | The text to filter.
| criteria | The criteria to use.
|===

=== Example

This example shows how `filter` can be used to remove all characters in odd positions.

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
output application/json
---
"hello world" filter ($$ mod 2) == 0

----

==== Output

[source,Json,linenums]
----
"hlowrd"
----


[[filter3]]
== filter&#40;@StreamCapable value: Null, criteria: &#40;item: Nothing, index: Nothing&#41; &#45;&#62; Any&#41;: Null

Helper function that enables `filter` to work with a `null` value.

