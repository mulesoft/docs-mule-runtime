= groupBy
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]



[[groupby1]]
== groupBy<T, R&#62;&#40;items: Array<T&#62;, criteria: &#40;item: T, index: Number&#41; &#45;&#62; R&#41;: { &#40;R&#41;: Array<T&#62; }

Returns an object that groups items from an array based on specified
criteria, such as an expression or matching selector.


This version of `groupBy` groups the elements of an array using the
`criteria` function. Other versions act on objects and handle null values.

=== Parameters

[%header, cols="1,3"]
|===
| Name   | Description
| `items` | The array to group.
| `criteria` | Expression providing the criteria by which to group the items in the array.
|===

=== Example

This example groups items from the input array `["a","b","c"]` by their
indices. Notice that it returns the numeric indices as strings and that items
(or values) of the array are returned as arrays, in this case, with a single
item each. The items in the array are grouped based on an anonymous function
`(item, index) -> index` that uses named parameters (`item` and `index`).
Note that you can produce the same result using the anonymous parameter
`&#36;&#36;` to identify the indices of the array like this:
`["a","b","c"] groupBy &#36;&#36;`

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
output application/json
---
["a","b","c"] groupBy (item, index) -> index
----

==== Output

[source,JSON,linenums]
----
{ "2": [ "c" ], "1": [ "b" ], "0": [ "a" ] }
----

=== Example

This example groups the elements of an array based on the language field.
Notice that it uses the `item.language` selector to specify the grouping
criteria. So the resulting object uses the "language" values (`"Scala"` and
`"Java"`) from the input to group the output. Also notice that the output
places the each input object in an array.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
var myArray = [
   { "name": "Foo", "language": "Java" },
   { "name": "Bar", "language": "Scala" },
   { "name": "FooBar", "language": "Java" }
]
output application/json
---
myArray groupBy (item) -> item.language
----

==== Output

[source,JSON,linenums]
----
{
  "Scala": [
    { "name": "Bar", "language": "Scala" }
  ],
  "Java": [
    { "name": "Foo", "language": "Java" },
    { "name": "FooBar", "language": "Java" }
  ]
}
----


=== Example

This example uses `groupBy "myLabels"`to return an object where `"mylabels"`
is the key, and an array of selected values
(`["Open New", "Zoom In", "Zoom Out", "Original View" ]`) is the value. It
uses the selectors (`myVar.menu.items.*label`) to create that array. Notice
that the selectors retain all values where `"label"` is the key but filter
out values where `"id"` is the key.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
var myVar = { menu: {
    header: "Move Items",
    items: [
        {"id": "internal"},
        {"id": "left", "label": "Move Left"},
        {"id": "right", "label": "Move Right"},
        {"id": "up", "label": "Move Up"},
        {"id": "down", "label": "Move Down"}
    ]
}}
output application/json
---
(myVar.menu.items.*label groupBy "myLabels")
----

==== Output

[source,JSON,linenums]
----
{ "myLabels": [ "Move Left", "Move Right", "Move Up", "Move Down" ] }
----


[[groupby2]]
== groupBy<R&#62;&#40;text: String, criteria: &#40;character: String, index: Number&#41; &#45;&#62; R&#41;: { &#40;R&#41;: String }

Returns an object that groups characters from a string based on specified
criteria, such as an expression or matching selector.


This version of `groupBy` groups the elements of an array using the
`criteria` function. Other versions act on objects and handle null values.


=== Parameters

[%header, cols="1,3"]
|===
| Name   | Description
| text | The string to group by.
| criteria | The criteria to use.
|===

=== Example

This example shows howyou can use `groupBy` to split a string into
vowels and not vowels.

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
output application/json
---
"hello world!" groupBy (not isEmpty($ find /[aeiou]/))

----

==== Output

[source,Json,linenums]
----
{
  "false": "hll wrld!",
  "true": "eoo"
}
----


[[groupby3]]
== groupBy<K, V, R&#62;&#40;object: { &#40;K&#41;?: V }, criteria: &#40;value: V, key: K&#41; &#45;&#62; R&#41;: { &#40;R&#41;: { &#40;K&#41;?: V } }

Groups elements of an object based on criteria that the `groupBy`
uses to iterate over elements in the input.


=== Parameters

[%header, cols="1,3"]
|===
| Name   | Description
| `object` | The object containing objects to group.
| `criteria` | The grouping criteria to apply to elements in the input object, such as a `key` and/or `value` of the object to use for grouping.
|===

=== Example

This example groups objects within an array of objects using the anonymous
parameter `&#36;` for the value of each key in the input objects. It applies
the DataWeave `upper` function to those values. In the output, these values
become upper-case keys. Note that you can also write the same example using
a named parameter for the within an anonymous function like this:
`{ "a" : "b", "c" : "d"} groupBy (value) -> upper(value)`

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
{ "a" : "b", "c" : "d"} groupBy upper($)
----

==== Output

[source,JSON,linenums]
----
{ "D": { "c": "d" }, "B": { "a": "b" } }
----

=== Example

This example uses `groupBy "costs"` to produce a JSON object from an XML object
where `"costs"` is the key, and the selected values of the XML element `prices`
becomes the JSON value (`{ "price": "9.99", "price": "10.99" }`).

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
var myRead =
read("<prices><price>9.99</price><price>10.99</price></prices>","application/xml")
output application/json
---
myRead.prices groupBy "costs"
----

==== Output

[source,JSON,linenums]
----
{ "costs" : { "price": "9.99", "price": "10.99" } }
----


[[groupby4]]
== groupBy&#40;value: Null, criteria: &#40;Nothing, Nothing&#41; &#45;&#62; Any&#41;: Null

Helper function that enables `groupBy` to work with a `null` value.

