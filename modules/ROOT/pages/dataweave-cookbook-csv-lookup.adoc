= Look Up Data in a CSV File
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]

This DataWeave example uses `filter` with `map` to look up data in a CSV file and return a country code for each calling code found within an input array of phone numbers. 
//LINK TO DW 1.0 EXAMPLES:
include::partial$dataweave1-links.adoc[tag=dataweave1Examples]

The following JSON payload provides an array of phone numbers to the DataWeave script.

[source,JSON,linenums]
----
[
   "54-112724555",
   "1-6298765432"
]
----

The value of the Mule variable `country_codes` is a CSV file that maps calling codes to country codes.

[source,CSV,linenums]
----
CALLING_CODE,COUNTRY_CODE
1,US
7,RU
54,AR
20,EG
32,BE
33,FR
505,NI
506,CR
1876,JM
1905,CA
1939,PR
262262,RE
262269,YT
.,.
.,.
----

The following DataWeave script reads each phone number in the payload and uses the `filter` function to search for the calling code within the Mule variable `country_code` that matches the calling code in the phone number. Then the `map` function gets the country code that corresponds to the calling code and returns the results to an array of JSON objects.

//MANUAL TESTING REQUIRED FOR NOW (NEED TO TEST WITH XLSX FILE)
.DataWeave Script:
[source,dataweave, linenums]
----
%dw 2.0
output application/json

fun lookupCountryCode(phoneNumber: String): String = do {
    var callingCode = (phoneNumber splitBy("-"))[0]
    --- 
    (vars.country_code filter ((item, index) -> item.CALLING_CODE == callingCode) map ((item, index) -> item.COUNTRY_CODE))[0]
}
---
payload map ((item, index) -> { phone: item, countryCode: lookupCountryCode(item)})
----

//MANUAL TESTING REQUIRED (FOR NOW)
.Sample Output (JSON):
[source,JSON,linenums]
----
[
  {
    "phone": "54-112724555",
    "countryCode": "AR"
  },
  {
    "phone": "1-6298765432",
    "countryCode": "US"
  }
]
----

== See Also

* xref:dataweave-formats.adoc[Supported Data Formats]
