= orderBy
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]



[[orderby1]]
== orderBy<K, V, R, O <: { &#40;K&#41;?: V }&#62;&#40;object: O, criteria: &#40;value: V, key: K&#41; &#45;&#62; R&#41;: O

Reorders the elements of an input using criteria that acts on selected
elements of that input.


This version of `orderBy` takes an object as input. Other versions act on an
input array or handle a null value.

Note that you can reference the index with the anonymous parameter
`&#36;&#36;` and the value with `&#36;`.

=== Parameters

[%header, cols="1,3"]
|===
| Name   | Description
| `object` | The object to reorder.
| `criteria` | The result of the function is used as the criteria to reorder the object.
|===

=== Example

This example alphabetically orders the values of each object in the input
array. Note that `orderBy($.letter)` produces the same result as
`orderBy($[0])`.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
[{ letter: "e" }, { letter: "d" }] orderBy($.letter)
----

==== Output

[source,JSON,linenums]
----
[
  {
    "letter": "d"
  },
  {
    "letter": "e"
  }
]
----

=== Example

The `orderBy` function does not have an option to order in descending order
instead of ascending. In these cases, you can simply invert the order of
the resulting array using `-`, for example:

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
orderDescending: ([3,8,1] orderBy -$)
----

==== Output

[source,JSON,linenums]
----
{ "orderDescending": [8,3,1] }
----


[[orderby2]]
== orderBy<T, R&#62;&#40;array: Array<T&#62;, criteria: &#40;item: T, index: Number&#41; &#45;&#62; R&#41;: Array<T&#62;

Sorts an array using the specified criteria.


=== Parameters

[%header, cols="1,3"]
|===
| Name   | Description
| `array` | The array  to sort.
| `criteria` | The result of the function will be used as the criteria to sort the list. It should return a simple value (String, Number, etc)
|===

=== Example

This example sorts an array of numbers based on the numeric values.

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
output application/json
---
[3,2,3] orderBy $
----

==== Output

[source,JSON,linenums]
----
[ 2, 3, 3 ]
----

=== Example

This example sorts an array of people based on their age.

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
output application/json
---
[{name: "Santiago", age: 42},{name: "Leandro", age: 29}, {name: "Mariano", age: 35}] orderBy (person) -> person.age
----

==== Output

[source,JSON,linenums]
----
[
  {
    name: "Leandro",
    age: 29
  },
  {
    name: "Mariano",
    age: 35
  },
  {
    name: "Santiago",
    age: 42
  }
]
----

=== Example

This example changes the order of the objects in a JSON array. The expression first orders them alphabetically by the value of the `Type` key, then reverses the order based on the `[-1 to 0]`.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
var myInput = [
    {
        "AccountNumber": "987999321",
        "NameOnAccount": "QA",
        "Type": "AAAA",
        "CDetail": {
            "Status": "Open"
        }
    },
    {
        "AccountNumber": "12399978",
        "NameOnAccount": "QA",
        "Type": "BBBB",
        "CDetail": {}
    },
    {
        "AccountNumber": "32199974",
        "NameOnAccount": "QA",
        "Type": "CCCC",
        "CDetail": {}
    }
]
output application/json
---
(myInput orderBy $.Type)[-1 to 0]
----

==== Output

[source,JSON,linenums]
----
[
  {
    "AccountNumber": "32199974",
    "NameOnAccount": "QA",
    "Type": "CCCC",
    "CDetail": {

    }
  },
  {
    "AccountNumber": "12399978",
    "NameOnAccount": "QA",
    "Type": "BBBB",
    "CDetail": {

    }
  },
  {
    "AccountNumber": "987999321",
    "NameOnAccount": "QA",
    "Type": "AAAA",
    "CDetail": {
      "Status": "Open"
    }
  }
]
----


[[orderby3]]
== orderBy&#40;value: Null, criteria: &#40;item: Nothing, index: Nothing&#41; &#45;&#62; Null&#41;: Null

Helper function that enables `orderBy` to work with a `null` value.

