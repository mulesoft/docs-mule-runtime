= now



[[now1]]
== now&#40;&#41;: DateTime

Returns a `DateTime` value for the current date and time.


=== Example

This example uses `now()` to return the current date and time as a
`DataTime` value.

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
output application/json
---
{ nowCalled: now() }
----

==== Output

[source,JSON,linenums]
----
{ "nowCalled": "2018-11-26T18:23:06.773-03:00" }
----

=== Example

This example shows uses of the `now()` function with valid
selectors. It also shows how to get the epoch time with `now() as Number`.
See also,
https://docs.mulesoft.com/mule-runtime/4.1/dataweave-selectors[DataWeave Selectors].

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
output application/json
---
{
  now: now(),
  epochTime : now() as Number,
  nanoseconds: now().nanoseconds,
  milliseconds: now().milliseconds,
  seconds: now().seconds,
  minutes: now().minutes,
  hour: now().hour,
  day: now().day,
  month: now().month,
  year: now().year,
  quarter: now().quarter,
  dayOfWeek: now().dayOfWeek,
  dayOfYear: now().dayOfYear,
  offsetSeconds: now().offsetSeconds
}
----

==== Output

[source,JSON,linenums]
----
{
  "now": "2018-07-23T22:03:04.829Z",
  "epochTime": 1533143270,
  "nanoseconds": 829000000,
  "milliseconds": 829,
  "seconds": 4,
  "minutes": 3,
  "hour": 22,
  "day": 23,
  "month": 7,
  "year": 2018,
  "quarter": 3,
  "dayOfWeek": 1,
  "dayOfYear": 204,
  "offsetSeconds": 0
}
----

