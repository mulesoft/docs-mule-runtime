= Merge Fields from Separate Objects
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]
:keywords: studio, anypoint, transform, transformer, format, aggregate, filter, json, metadata, dataweave, data weave, datamapper, dwl, dfl, dw, output structure, input structure, map, mapping, contains, as

The DataWeave examples merge fields from separate input arrays. The first   (`firstInput`) is a DataWeave variable that lists price by book ID, and the second (`secondInput`) lists authors by book ID. Each book has a unique `bookId` key.

The DataWeave scripts produce the same output. Both scripts use two `map` functions with a filter, and one of them also creates an alias for the `bookId`:
* The first `map` function iterates over the elements of the `firstInput`
array. After the function evaluates a given `price` field in `firstInput`, a second `map` function uses a filter to identify elements in the second payload (`secondInput`) that match the `filter` criteria (`secondInput filter ($.*bookId contains id)`). The filter returns an element in `secondInput` that contains a `bookId` value that matches the `bookId` value in the `firstInput` element.
* The second `map` function evaluates that filtered element and then uses `secondInputValue.author` to select the value of its `"author"` field in the object `{author : secondInputValue.author}`.
* `filter` limits the scope of the second `map` function to objects in the `secondInput` that share the same `bookId`.
* `using` defines a shorter alias within the scope of the main `map` function. The second DataWeave script uses `using`. The first does not.

.DataWeave Script:
[source,dataweave,linenums]
----
%dw 2.0
var firstInput = [
  { "bookId":"101",
    "title":"world history",
    "price":"19.99"
  },
  {
    "bookId":"202",
    "title":"the great outdoors",
    "price":"15.99"
  }
]
var secondInput = [
  {
    "bookId":"101",
    "author":"john doe"
  },
  {
    "bookId":"202",
    "author":"jane doe"
  }
]
output application/json
---
firstInput map (firstInputValue) ->
  {
    theId : firstInputValue.bookId as Number,
    theTitle: firstInputValue.title,
    thePrice: firstInputValue.price as Number,
    (secondInput filter ($.*bookId contains firstInputValue.bookId) map (secondInputValue) -> {
      theAuthor : secondInputValue.author
    })
  }
----

As the next script shows, you can also write the same script using an `id` alias (created with `using (id = firstInputValue.bookId)`). The alias replaces the  selector expression, `firstInputValue.bookId`, which is longer.

.DataWeave Script with Alias:
[source,dataweave,linenums]
----
%dw 2.0
var firstInput = [
  { "bookId":"101",
    "title":"world history",
    "price":"19.99"
  },
  {
    "bookId":"202",
    "title":"the great outdoors",
    "price":"15.99"
  }
]
var secondInput = [
  {
    "bookId":"101",
    "author":"john doe"
  },
  {
    "bookId":"202",
    "author":"jane doe"
  }
]
output application/json
---
firstInput map (firstInputValue) -> using (id = firstInputValue.bookId)
  {
    theValue : id as Number,
    theTitle: firstInputValue.title,
    thePrice: firstInputValue.price as Number,
    (secondInput filter ($.*bookId contains id)  map (secondInputValue) -> {
      theAuthor : secondInputValue.author
    })
  }
----

Both scripts produce the same output.

.Output
[source,json,linenums]
----
[
  {
    "theId": 101,
    "theTitle": "world history",
    "thePrice": 19.99,
    "theAuthor": "john doe"
  },
  {
    "theId": 202,
    "theTitle": "the great outdoors",
    "thePrice": 15.99,
    "theAuthor": "jane doe"
  }
]
----

== Related Examples

* xref:dataweave-cookbook-extract-data.adoc[Extract Data]

* xref:dataweave-cookbook-reference-multiple-inputs.adoc[Reference Multiple Inputs]

* xref:dataweave-cookbook-zip-arrays-together.adoc[Zip Arrays Together]

== See Also

* xref:dataweave-selectors.adoc[DataWeave Selectors]

* xref:dataweave-cookbook.adoc[DataWeave Cookbook]
