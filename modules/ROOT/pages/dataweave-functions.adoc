= Defining DataWeave Functions
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]

////
You define a function using the fun declaration in the header section. Like other programming languages, DataWeave functions allow you to refactor DataWeave expressions into reusable code blocks. In DataWeave, every code block is an expression that is immediately evaluated when it is called from the DataWeave body expression. Functions can define and use one or more arguments.
////

You can define your own DataWeave functions using the `fun` declaration in the header of a DataWeave script. For example, this a simple DataWeave function accepts a single String argument that outputs `"HELLO"`:

[source,dataweave,linenums]
----
%dw 2.0
output application/json
fun toUpper(aString) = upper(aString)
---
toUpper("hello")
----

The argument to a DataWeave function can be any DataWeave expression. This function also outputs `"HELLO"`:

[source,dataweave,linenums]
----
%dw 2.0
output application/json
fun toUpper(aString) = upper(aString)
---
toUpper("h" ++ "el" ++ lower("LO") )
----

To test the type of the argument passed into your function, you can perform xref:dataweave-pattern-matching.adoc[pattern matching] with the built-in `match` operation.

.Example: Function that Uses Pattern Matching
[source,dataweave,linenums]
----
%dw 2.0
output application/json
fun toUpper(aString)
= aString match {
  case is String -> upper(aString)
  else -> null
}
---
toUpper("h" ++ "el" ++ lower("LO") )
----

This example creates a function that reformats a numeric string into a common phone number format:

.Example: toPhoneFormat() Function
[source,dataweave,linenums]
----
%dw 2.0
output application/json
fun toPhoneFormat(str: String) =
    "(" ++ str[0 to 2] ++ ") " ++ str[3 to 5] ++ "-" ++ str[6 to 9]
---
toPhoneFormat("1234567890")
----

.Output of toPhoneFormat("1234567890")
----
"(123) 456-7890"
----

== See Also

xref:dataweave-create-module.adoc[Creating Custom Modules and Mappings]

xref:dw-functions.adoc[DataWeave Function Reference]