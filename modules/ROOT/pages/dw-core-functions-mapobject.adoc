= mapObject
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]

//* <<mapobject1>>
//* <<mapobject2>>


[[mapobject1]]
== mapObject({ (K)?: V }, (V, K, Number) -> Object): Object

Iterates through key-value pairs within an object and returns an object. You
can retrieve the key, value, or index of any key-value pair in the object.

This function is similar to `map`. However, instead of processing only values
of an object, `mapObject` processes both keys and values as a tuple. Also,
instead of returning an array with the results of processing these values
through the function, it returns an object, which consists of a list of the
key-value pairs that result from processing both key and value of the object
through the function (a lambda).

The function is invoked with three parameters: `value`, `key` and the `index`.
The third parameter, `index`, is optional.

.Transform
[source,dataweave,linenums]
----
%dw 2.0
output application/json
var conversionRate=13
---
priceList: payload.prices mapObject(value, key, index) -> {
  (key) : {
       dollars: value,
       localCurrency: value * conversionRate,
       index_plus_1: index + 1
   }
}
----

[[input_mapobject]]
.Input
[source,xml,linenums]
----
<prices>
    <basic>9.99</basic>
    <premium>53</premium>
    <vip>398.99</vip>
</prices>
----

[[output]]
.Output
[source,json,linenums]
----
{
  'priceList': {
    'basic': {
      'dollars': '9.99',
      'localCurrency': 129.87,
      'index_plus_1': 1
    },
    'premium': {
      'dollars': '53',
      'localCurrency': 689,
      'index_plus_1': 2
    },
    'vip': {
      'dollars': '398.99',
      'localCurrency': 5186.87,
      'index_plus_1': 3
    }
  }
}
----

For each key-value pair in the input in the example, the key is preserved,
and the value becomes an object with two properties: the original value and
the result of multiplying the original value by a constant, a `var` that is
defined as a directive in the header of the DataWeave script.

Important:
When you use a parameter to populate one of the keys of your output, you must
either enclose the parameter in parentheses (for example, `(key)`), or
you need to prepend it with a `&#36;` and enclose it in quotation marks (for
example, `'&#36;key'`, as shown above). Otherwise, the name of the property is
treated as a literal string.

If you do not name the parameters, you need to reference them through
placeholders: `&#36;` for the value, `&#36;&#36;` for the key, and `&#36;&#36;&#36;` for the index,
for example:

.Transform
----
%dw 2.0
output application/xml
---
{ 'price' : 9 } mapObject('$$' : $)
----

.Output
----
<?xml version='1.0' encoding='UTF-8'?>
<price>9</price>
----

The next example incorporates each index of the //* <<input_mapobject, price input>>
above, as well as the input keys and values.

.Transform
----
%dw 2.0
output application/json
---
priceList: payload.prices mapObject(
 ($$) : { '$$$' : $ }
)
----

Notice that the index is surrounded in quotes (`'&#36;&#36;&#36;'`) because this Numeric
key must be coerced to a String and cannot be a Number. Alternatively, you
could write `'&#36;&#36;&#36;'` as `(&#36;&#36;&#36; as String)`.

When the preceding script receives the //* <<input_mapobject, price input>> above, it
produces the following output:

.Output
----
{
  'priceList': {
    'basic': {
      '0': '9.99'
    },
    'premium': {
      '1': '53'
    },
    'vip': {
      '2': '398.99'
    }
  }
}
----

The next example returns the same output as the first //* <<output, `mapObject`
example>> above, which explicitly invokes the named parameters
`(value, key, index)`.

.Transform
----
%dw 2.0
output application/json
var conversionRate=13
---
priceList: payload.prices mapObject(
 ($$): {
   dollars: $,
   localCurrency: $ * conversionRate,
   index_plus_1: $$$ + 1
 }
)
----

When you use a parameter to populate one of the keys of your output,
as with the case of `&#36;&#36;`, you must either enclose it in quote marks
(for example, `'&#36;&#36;'`) or parentheses (for example, `(&#36;&#36;)`). Both
are equally valid.



[[mapobject2]]
== mapObject(Null, (Any, Any, Number) -> Any): Null

Helper function that allows `mapObject` to work with null values.