= reduce
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]



[[reduce1]]
== reduce&#40;Array<T&#62;, &#40;item: T, accumulator: T&#41; &#45;&#62; T&#41;: T &#124; Null

Applies a reduction expression to the elements in an array.


Note that if the array is empty and no default value is set on the
accumulator parameter, a null value is returned.

=== Parameters

[%header, cols="1,3"]
|===
| Name   | Description
| `item` | Item in the input array. It provides the value to reduce. Can also be referenced as `&#36;`.
| `acc` | An accumulator (also referenced as `&#36;&#36;`) to apply to the
result of the reduction to the input array.
|===

=== Example

This example returns the sum of the numeric values in the first input array.

==== Source

[source,DataWeave,linenums]
----
%dw 2.0
var myList =
output application/json
---
[2, 3] reduce ($ + $$)
----

==== Output

[source,JSON,linenums]
----
5
----

=== Example

This example adds the numbers in the `sum` example, concatenates the same
numbers in `concat`, and shows that an empty array `[]` (defined in
`myEmptyList`) returns `null` in `emptyList`.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
var myNums = [1,2,3,4]
var myEmptyList = []
output application/json
---
{
   "sum" : myNums reduce ($$ + $),
   "concat" : myNums reduce ($$ ++ $),
   "emptyList" : myEmptyList reduce ($$ ++ $)
}
----

==== Output

[source,JSON,linenums]
----
{ "sum": 10, "concat": "1234", "emptyList": null }
----

=== Example

This example sets the first element from the first input array to `"z"`, and
it adds `3` to the sum of the second input array. In `multiply`, it shows how
to multiply each value in an array by the next
(`[2,3,3] reduce ((item, acc) -> acc * item)`) to
produce a final result of `12` (= `2 * 3 * 3`). The final example,
`multiplyAcc`, sets the accumulator to `3` to multiply the result of
`acc * item` (= `12`) by `3` (that is, `3 (2 * 2  * 3) = 36`), as shown in
the output.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
---
{
   "concat" : ["a", "b", "c", "d"] reduce ((item, acc = "z") -> acc ++ item),
   "sum": [0, 1, 2, 3, 4, 5] reduce ((item, acc = 3) -> acc + item),
   "multiply" : [2,3,3] reduce ((item, acc) -> acc * item),
   "multiplyAcc" : [2,2,3] reduce ((item, acc = 3) -> acc * item)
}
----

==== Output

[source,JSON,linenums]
----
{ "concat": "zabcd", "sum": 18, "multiply": 12, "multiplyAcc": 36 }
----

=== Example

This example shows a variety of uses of `reduce`, including its application to
arrays of boolean values and objects.

==== Source

[source,DataWeave, linenums]
----
%dw 2.0
output application/json
var myVar =
{
  "a": [0, 1, 2, 3, 4, 5],
  "b": ["a", "b", "c", "d", "e"],
  "c": [{ "letter": "a" }, { "letter": "b" }, { "letter": "c" }],
  "d": [true, false, false, true, true]
}
---
{
  "a" : [0, 1, 2, 3, 4, 5] reduce $$,
  "b": ["a", "b", "c", "d", "e"] reduce $$,
  "c": [{ "letter": "a" }, { "letter": "b" }, { "letter": "c" }] reduce ((val, acc = "z") -> acc ++ val.letter),
  "d": [{ letter: "a" }, { letter: "b" }, { letter: "c" }] reduce $$,
  "e": [true, false, false, true, true] reduce ($$ and $),
  "f": [true, false, false, true, true] reduce ((val, acc) -> acc and val),
  "g": [true, false, false, true, true] reduce ((val, acc = false) -> acc and val),
  "h": [true, false, false, true, true] reduce $$,
  "i": myVar.a reduce ($$ + $),
  "j": myVar.a reduce ((val, acc) -> acc + val),
  "k": myVar.a reduce ((val, acc = 3) -> acc + val),
  "l": myVar.a reduce $$,
  "m": myVar.b reduce ($$ ++ $),
  "n": myVar.b reduce ((val, acc) -> acc ++ val),
  "o": myVar.b reduce ((val, acc = "z") -> acc ++ val),
  "p": myVar.b reduce $$,
  "q": myVar.c reduce ((val, acc = "z") -> acc ++ val.letter),
  "r": myVar.c reduce $$,
  "s": myVar.d reduce ($$ and $),
  "t": myVar.d reduce ((val, acc) -> acc and val),
  "u": myVar.d reduce ((val, acc = false) -> acc and val),
  "v": myVar.d reduce $$,
  "w": ([0, 1, 2, 3, 4] reduce ((val, acc = {}) -> acc ++ { a: val })) pluck $,
  "x": [] reduce $$,
  "y": [] reduce ((val,acc = 0) -> acc + val)
}
----

==== Output

[source,JSON,linenums]
----
"a": 0,
"b": "a",
"c": "zabc",
"d": { "letter": "a" },
"e": false,
"f": false,
"g": false,
"h": true,
"i": 15,
"j": 15,
"k": 18,
"l": 0,
"m": "abcde",
"n": "abcde",
"o": "zabcde",
"p": "a",
"q": "zabc",
"r": { "letter": "a" },
"s": false,
"t": false,
"u": false,
"v": true,
"w": [ 0,1,2,3,4 ],
"x": null,
"y": 0
}
----


[[reduce2]]
== reduce&#40;Array<T&#62;, &#40;item: T, accumulator: A&#41; &#45;&#62; A&#41;: A



