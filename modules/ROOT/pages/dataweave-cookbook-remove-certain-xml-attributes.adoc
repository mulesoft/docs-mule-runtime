= Remove Specified XML Attributes
ifndef::env-site,env-github[]
include::_attributes.adoc[]
endif::[]
:keywords: studio, anypoint, transform, transformer, format, aggregate, rename, split, filter convert, xml, json, csv, pojo, java object, metadata, dataweave, data weave, datamapper, dwl, dfl, dw, output structure, input structure, map, mapping

You might want to remove specific attributes from within an XML tag that are known to contain sensitive data. The DataWeave example defines a function in the DataWeave header that removes a specific XML attribute from the passed element and its children.



This example uses:

* `mapObject` to go through each element.
* `@` to refer to the attributes in an XML element.
* `if` and `else` to conditionally only list XML attributes when they are present, and also to only recursively call the function when a child element is an object.
* `is` to check if each child is of type `Object` .
* `-` to remove an element from the list of XML attributes.
* `inlineCloseOn="empty"` on the output directive, so that the output XML displays self-closing tags when there are no values.




.DataWeave
[source,dataweave,linenums]
----
%dw 2.0
input payload application/json
output application/xml inlineCloseOn="empty"
var removeAttribute = (element,attrName) ->
  element mapObject (value, key) -> {
    (key) @(
        (
          if (key.@?)
            (key.@ - attrName)
          else {}
        )
    ) :
    if (value is Object)
      removeAttribute(value, attrName)
    else value
  }
---
removeAttribute(payload, "password")
----

.Input
[source,xml,linenums]
----
<users>
  <user username="Julian" password="1234"/>
  <user username="Mariano" password="4321"/>
</users>
----

.Output
[source,xml,linenums]
----
<?xml version='1.0' encoding='US-ASCII'?>
<users>
  <user username="Julian"/>
  <user username="Mariano"/>
</users>
----

== Related Examples


* xref:dataweave-cookbook-output-self-closing-xml-tags.adoc[Output Self-closing XML tags]

* xref:dataweave-cookbook-insert-attribute.adoc[Insert an Attribute into an XML Tag]

* xref:dataweave-cookbook-include-xml-namespaces.adoc[Include XML Namespaces]

* xref:dataweave-cookbook-exclude-field.adoc[Exclude Fields from the Output]

* xref:dataweave-cookbook-output-a-field-when-present.adoc[Output a Field When Present]

* xref:dataweave-cookbook-conditional-list-reduction-via-function.adoc[Conditionally Reduce a List through a Function]


== See Also

* xref:dataweave-selectors.adoc[DataWeave Selectors]
* xref:dataweave-cookbook.adoc[DataWeave Cookbook]
